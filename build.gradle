plugins {
    id "java-gradle-plugin"
    id "maven-publish"
    id "idea"
    id "jacoco"
    id "com.gradle.plugin-publish" version "0.10.1"
    id 'org.ajoberstar.reckon' version "0.9.0"
    id 'com.github.johnrengelman.shadow' version '4.0.4' apply false
    id "org.sonarqube" version "2.7.1"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

group 'eu.xenit.gradle'

sourceCompatibility = 1.8

configurations {
    packaged
    compileOnly.extendsFrom(packaged)
    testCompile.extendsFrom(packaged)
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    main {
        java {
            runtimeClasspath += configurations.packaged
        }
    }
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.sourceDirectories
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
    }
}


gradlePlugin {
    plugins {
        dockerAlfresco {
            id = "eu.xenit.docker-alfresco"
            implementationClass = "eu.xenit.gradle.alfresco.DockerAlfrescoPlugin"
        }
        dockerConfig {
            id = "eu.xenit.docker-config"
            implementationClass = "eu.xenit.gradle.docker.DockerConfigPlugin"
        }
        docker {
            id = "eu.xenit.docker"
            implementationClass = "eu.xenit.gradle.docker.DockerPlugin"
        }
    }
}

pluginBundle {
    vcsUrl = "https://github.com/xenit-eu/alfresco-docker-gradle-plugin"
    website = vcsUrl
    description = "A gradle plugin to create Alfresco docker images with extensions"
    tags = ["alfresco", "docker"]
    plugins {
        dockerAlfresco {
            displayName = "Alfresco docker plugin"
        }

        dockerConfig {
            displayName = "Docker configuration plugin"
        }

        docker {
            displayName = "Docker plugin"
        }
    }

    mavenCoordinates {
        groupId = "eu.xenit.gradle"
        artifactId = "alfresco-docker-plugin"
    }
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
    maven {
        url "https://artifacts.alfresco.com/nexus/content/groups/public/"
    }
}


dependencies {
    packaged 'org.alfresco:alfresco-mmt:5.2.g'
    compile 'com.bmuschko:gradle-docker-plugin:6.1.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile 'com.avast.gradle:gradle-docker-compose-plugin:0.8.12'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.5'
    compile 'org.eclipse.jgit:org.eclipse.jgit:4.6.0.201612231935-r'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile gradleTestKit()
    testCompile "org.mockito:mockito-core:2.+"
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

task classesJar(type: Jar) {
    from sourceSets.main.output
    classifier = "classes"
}

task shadowJar(type: ShadowJar, dependsOn: classesJar) {
    configurations = [project.configurations.packaged]
    classifier = "shadow"
}

jar {
    from zipTree(shadowJar.outputs.files.singleFile)
    dependsOn(shadowJar)
}

import org.gradle.util.GradleVersion

task integrationTest(type: Test, group: "verification") {
    useJUnit()
    if (project.hasProperty("integrationTestGradleVersions")) {
        // This is used for sharding integration tests across different gradle versions
        // to avoid a timeout on travis because the total build takes too long.
        // See also the .travis.yml file
        project.property("integrationTestGradleVersions").tokenize(",").forEach({ version ->
            // The versions specified in `integrationTestGradleVersions` are major.minor.
            // We only want it to match those, and eventual patches to those versions.
            // In particular, we want 4.1 to match 4.1.2, but not 4.10.
            // Since there is only one test parameter for every version, either the first
            // pattern matches major.minor, or the second pattern matches major.minor.patch
            filter.includeTestsMatching("*[Gradle v${version}]")
            filter.includeTestsMatching("*[Gradle v${version}.*]")
        })
    }
    doFirst {
        if (gradle.startParameter.offline) {
            systemProperty "eu.xenit.gradle.integration.useGradleVersion", GradleVersion.current().version
        }
    }
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter(test)
}

jacocoTestReport {
    dependsOn(test)
    mustRunAfter(integrationTest)
    executionData(test, integrationTest)
    reports {
        xml.enabled = true
    }
}
check.dependsOn(jacocoTestReport)

sonarqube {
    properties {
        properties["sonar.tests"] += sourceSets.integrationTest.java.srcDirs
    }
}

check.dependsOn(integrationTest)
reckonTagCreate.dependsOn(check)
tasks.sonarqube.dependsOn(check)

