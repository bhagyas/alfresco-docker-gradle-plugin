plugins {
    id 'eu.xenit.docker-alfresco'
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://artifacts.alfresco.com/nexus/content/groups/public/"
    }
}
dependencies {
    baseAlfrescoWar "org.alfresco:content-services-community:6.0.a@war"
}

dockerBuild {
    alfresco {
        // Base image used in the FROM of the docker build. Should be a compatible image.
        baseImage = "tomcat:7-jre8"
    }
}

createDockerFile {
    smartCopy file("test.sh"), "/"
    runCommand "mkdir -p /opt/gradle/test-src-1 /opt/gradle/test-src-2 /opt/gradle/test-src-3"
    smartCopy "build.gradle", "/opt/build.gradle"
    smartCopy files("directory"), "/opt/gradle/test-src-1"
    smartCopy "directory", "/opt/gradle/test-src-2"
    smartCopy "directory", "/opt/gradle/test-src-3/"
    runCommand "chmod +x /test.sh"
}

import com.bmuschko.gradle.docker.tasks.container.*

task createContainer(type: DockerCreateContainer) {
    targetImageId buildDockerImage.imageId
    cmd = ["/test.sh"]
    dependsOn(buildDockerImage)
}

task startContainer(type: DockerStartContainer) {
    targetContainerId createContainer.containerId
    dependsOn(createContainer)
}

task waitContainer(type: DockerWaitContainer) {
    targetContainerId createContainer.containerId
    dependsOn(startContainer)

    doLast {
        if (exitCode != 0) {
            throw new GradleException("Container did not exit with code 0");
        }
    }
}
